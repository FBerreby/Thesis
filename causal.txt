% ---------------------------------------------------------------- %
% -------------------- Causal Model  ----------------------------- %
% ---------------------------------------------------------------- %
% ---------------------------------------------------------------- %
% -------------- Event-Based Causality --------------------------- %
% ---------------------------------------------------------------- %

rel(R):-supRel(R).
rel(R):-oppRel(R).
allSupRel(R):-supRel(R).
allSupRel(R):-supRelTrans(R).
allOppRel(R):-oppRel(R).
allOppRel(R):-oppRelTrans(R).

% -------------- Supporting causality ---------------------------- %

supRel(causes;enables).
r(S,causes,F,T,U):-holds(S,F,T),prec(F,U),occurs(S,U,T),auto(U).
r(S,causes,E,T,F):-occurs(S,E,T),effect(E,F),holds(S,F,T+1).
r(S,R,E1,T1,E2):-r(S,causes,E1,T1,C),r(S,R,C,T2,E2),event(E1;E2),T2>T1,rel(R).
r(S,causes,E,T,E):-occurs(S,E,T).

r(S,enables,F,T,A):-holds(S,F,T),prec(F,A),occurs(S,A,T),action(A).
r(S,enables,F,T,omit(G,T)):-holds(S,F,T),prec(F,act(G,X)),complete(S,act(G,X),T),occurs(S,omit(G,T),T).

% -------------- Opposing causality ------------------------------ %

hyp(F1,F2):-prec(F1,U),auto(U),effect(U,F2).
hyp(F1,F3):-hyp(F1,F2),hyp(F2,F3).
canArise(S,U):-auto(U),occurs(S,U,T). 
canArise(S,I):-volition(I),complete(S,I,T). 
transTerm(S,E,P,T):-occurs(S,E,T),effect(E,neg(P)),holds(S,neg(P),T+1).
transTerm(S,E,neg(P),T):-occurs(S,E,T),effect(E,P),holds(S,P,T+1),positiveFluent(P).
transTerm(S,E,F2,T):-transTerm(S,E,F1,T),hyp(F1,F2). 
didHold(S,F,E,T):-holds(S,F,T),prec(F,E),nonInertial(F).
irrelevant(S,E1,T1,E2,T2):-transTerm(S,E1,F1,T1),prec(F1,E2),prec(F2,E2),not transTerm(S,E1,F2,T1),not holds(S,F2,T2),T1<T2,time(T2),not didHold(S,F2,E2,T1).
relevant(S,E1,T1,E2):-transTerm(S,E1,F1,T1),prec(F1,E2),not irrelevant(S,E1,T1,E2,T2),T1<T2,time(T2).

oppRel(prevents;excludes).
r(S,prevents,E,T,U):-transTerm(S,E,F,T),prec(F,U),relevant(S,E,T,U),not canArise(S,U),not omission(E),auto(U).
r(S,excludes,E,T,A):-transTerm(S,E,F,T),prec(F,A),relevant(S,E,T,A),not canArise(S,A),not omission(E),action(A).

% -------------- Choosing a volition ----------------------------- %

r(S,excludes,I1,T1,I2):-occurs(S,I1,T1),complete(S,I2,T1),not complete(S,I2,T2),volition(I1;I2),T1<T2,time(T2),I1!=I2.

% -------------- Transitivity ------------------------------------ %

supRelTrans(helps(enables);impedes(prevents;excludes)).
r(S,helps(enables),E1,T1,E3):-r(S,enables,E1,T1,E2),r(S,R,E2,T2,E3),supRel(R),event(E1;E3),not r(S,causes;enables,E1,T1,E3),T2>T1.
r(S1,impedes(prevents),E1,T1,E3):-r(S1,prevents,E1,T1,E2),r(S2,prevents,E2,T2,E3),occurs(S1,E3,T3),sameHistory(T1,S1,S2),T1<=T2,T1<T3,E1!=E3.
r(S1,impedes(excludes),E1,T1,E3):-r(S1,prevents,E1,T1,E2),r(S2,excludes,E2,T2,E3),occurs(S1,E3,T3),sameHistory(T1,S1,S2),T1<=T2,T1<T3,E1!=E3.
r(S1,impedes(excludes),E1,T1,E3):-r(S1,excludes,E1,T1,E2),r(S2,prevents,E2,T2,E3),occurs(S1,E3,T3),sameHistory(T1,S1,S2),T1<=T2,T1<T3,E1!=E3.
r(S1,impedes(excludes),E1,T1,E3):-r(S1,excludes,E1,T1,E2),r(S2,excludes,E2,T2,E3),occurs(S1,E3,T3),sameHistory(T1,S1,S2),T1<=T2,T1<T3,E1!=E3.

oppRelTrans(helps(excludes);impedes(enables)).
r(S,helps(excludes),E1,T1,E3):-r(S,enables,E1,T1,E2),r(S,R,E2,T2,E3),oppRel(R),event(E1;E3),not r(S,prevents;excludes,E1,T1,E3),T2>T1.
r(S1,impedes(enables),E1,T1,E3):-r(S1,excludes,E1,T1,E2),r(S2,causes,E2,T2,E3),not canArise(S1,E3),sameHistory(T1,S1,S2),T1<=T2,event(E3).
r(S1,impedes(enables),E1,T1,E3):-r(S1,excludes,E1,T1,E2),r(S2,enables,E2,T2,E3),not canArise(S1,E3),sameHistory(T1,S1,S2),T1<=T2,event(E3).

r(S1,excludes,E1,T1,E3):-r(S1,prevents,E1,T1,E2),r(S2,enables,E2,T2,E3),T1<=T2,not canArise(S1,E3),sameHistory(T1,S1,S2),event(E3).

% -------------- Same History Problem ---------------------------- %

differentHistory(T2,S1,S2):-occurs(S1,E,T1),not occurs(S2,E,T1),sim(S2),event(E),time(T2),T1<T2.
differentHistory(T,S2,S1):-differentHistory(T,S1,S2).
sameHistory(T,S1,S2):-not differentHistory(T,S1,S2),time(T),sim(S1;S2).

% -------------- Meta Transitivity ------------------------------- %

relTrans(R):-supRelTrans(R).
relTrans(R):-oppRelTrans(R).
r(S,R1,E1,T1,E3):-r(S,enables,E1,T1,E2),r(S,R1,E2,T2,E3),relTrans(R1),event(E1;E3),T1<T2.

% ---------------------------------------------------------------- %
% -------------- Scenario-Based Causality ------------------------ %
% ---------------------------------------------------------------- %

% -------------- Simple Counter-Factual validity ----------------- %

unequalFuture(T2,S1,S2):-occurs(S1,A,T1),complete(S2,A,T1),not occurs(S2,A,T1),action(A),time(T2),T2<T1.
unequalFuture(T2,S1,S2):-not occurs(S1,A,T1),occurs(S2,A,T1),sim(S1),action(A),time(T2),T2<T1.
equalFuture(T,S1,S2):-not unequalFuture(T,S1,S2),time(T),sim(S1;S2).
ovSim(S1,T,A1,S2):-occurs(S1,A1,T),occurs(S2,O,T),sameHistory(T,S1,S2),equalFuture(T,S1,S2),action(A1),omission(O).
uncertainty(S,T):-complete(S,A1,T),complete(S,A2,T),A1!=A2,action(A1;A2).
ovSim(S1,T,O,S2):-occurs(S1,O,T),occurs(S2,A,T),sameHistory(T,S1,S2),equalFuture(T,S1,S2),action(A),omission(O),not uncertainty(S1,T).

notValid(S1,R,I,T,E):-r(S1,R,I,T,E),ovSim(S1,T,I,S2),canArise(S2,E),volition(I),event(E),allSupRel(R). 
notValid(S1,R,I,T,E):-r(S1,R,I,T,E),ovSim(S1,T,I,S2),not canArise(S2,E),volition(I),event(E),allOppRel(R).
valid(S1,R,I,T,E):-r(S1,R,I,T,E),ovSim(S1,T,I,S2),not notValid(S1,R,I,T,E),volition(I),event(E). 

% -------------- Overdetermination ------------------------------- %

samePolarity(R1,R2):-allOppRel(R1;R2).
samePolarity(R1,R2):-allSupRel(R1;R2).
sameAgent(act(G,X1),act(G,X2)):-occurs(S1,act(G,X1),T1),occurs(S2,act(G,X2),T2).
overdeterminedBySelf(S,R1,A1,A2,T1,E):-r(S,R1,A1,T1,E),r(S,R2,A2,T2,E),{valid(S,R3,A1,T1,A2):allSupRel(R3)}0,{valid(S,R3,A2,T2,A1):allSupRel(R3)}0,samePolarity(R1,R2),sameAgent(A1,A2),action(A1;A2),A1!=A2.
overdeterminedByOthers(S,R1,A1,A2,T1,E):-r(S,R1,A1,T1,E),r(S,R2,A2,T2,E),{valid(S,R3,A1,T1,A2):allSupRel(R3)}0,{valid(S,R3,A2,T2,A1):allSupRel(R3)}0,samePolarity(R1,R2),not sameAgent(A1,A2),action(A1;A2),A1!=A2.

% -------------- Preemption -------------------------------------- %

preemptedBySelf(S1,R,A1,A2,T1,E):-r(S1,R,A1,T1,E),r(S2,R,A2,T2,E),ovSim(S1,T1,A1,S2),sameAgent(A1,A2),action(A1;A2),T1<=T2.
preemptedByOthers(S1,R,A1,A2,S2,T1,E):-r(S1,R,A1,T1,E),r(S2,R,A2,T2,E),ovSim(S1,T1,A1,S2),not sameAgent(A1,A2),action(A1;A2),T1<=T2.

%replaces(act(iris,stop),act(driver,stop)).
%replaces(act(driver,stop),act(iris,stop)).
%:-not occurs(S,act(iris;driver,stop),T),complete(S,act(iris;driver,stop),T).
%unequalFuture(T2,S1,S2):-not occurs(S1,A1,T1),occurs(S2,A1,T1),sim(S1),action(A1;A2),time(T2),T2<T1,{occurs(S1,A1,T1),replaces(A2,A1)}0.
%ovSim(S1,T,A1,S2):-occurs(S1,A1,T),occurs(S2,A2,T),sameHistory(T,S1,S2),equalFuture(T,S1,S2),action(A1;A2),replaces(A1,A2).

% ------------ Cruciality ---------------------------------------- %

laterSim(exc,S1,I,S2):-occurs(S1,I,T),not occurs(S2,I,T),sameHistory(T,S1,S2),volition(I),sim(S2).

notCrucial(S1,R,I,T,E):-r(S1,R,I,T,E),laterSim(exc,S1,I,S2),canArise(S2,E),volition(I),event(E),allSupRel(R).
notCrucial(S1,R,I,T,E):-r(S1,R,I,T,E),laterSim(exc,S1,I,S2),not canArise(S2,E),volition(I),event(E),allOppRel(R).
crucial(S1,R,I,T,E):-r(S1,R,I,T,E),not notCrucial(S1,R,I,T,E),volition(I),event(E).

% ------------ Extrinsic Necessity ------------------------------- %

notNecessary(S1,R,exc,I,T,E):-r(S1,R,I,T,E),laterSim(exc,S1,I,S2),not canArise(S2,E),volition(I),event(E),allSupRel(R).
notNecessary(S1,R,exc,I,T,E):-r(S1,R,I,T,E),laterSim(exc,S1,I,S2),canArise(S2,E),volition(I),event(E),allOppRel(R).
necessary(S1,R,exc,I,T,E):-r(S1,R,I,T,E),not notNecessary(S1,R,exc,I,T,E),volition(I),event(E).

% ------------ Elicited Necessity -------------------------------- %

laterSim(inc,S1,I,S2):-occurs(S1,I,T),occurs(S2,I,T),sameHistory(T,S1,S2),volition(I).

notNecessary(S1,R,inc,I,T,E):-r(S1,R,I,T,E),laterSim(inc,S1,I,S2),not canArise(S2,E),volition(I),event(E),allSupRel(R).
notNecessary(S1,R,inc,I,T,E):-r(S1,R,I,T,E),laterSim(inc,S1,I,S2),canArise(S2,E),volition(I),event(E),allOppRel(R).
necessary(S1,R,inc,I,T,E):-r(S1,R,I,T,E),not notNecessary(S1,R,inc,I,T,E),volition(I),event(E).

% ------------ Related Works ------------------------------------- %

activeCR(S,R,A,T,U):-necessary(S,R,inc,A,T,U),notNecessary(S,R,exc,A,T,U),action(A),auto(U).
sameAgent(omit(G,X1),act(G,X2)):-occurs(S1,omit(G,X1),T1),occurs(S2,act(G,X2),T2).
passiveCR(S1,occurence,I,T1,U):-occurs(S1,I,T1),occurs(S1,U,T2),T1<T2,necessary(S2,R,inc,A,T1,U),allOppRel(R),sameHistory(T1,S1,S2),sameAgent(I,A),auto(U),volition(I),action(A),I!=A.
passiveCR(S1,nonOccurrence,I,T1,U):-occurs(S1,I,T1),not occurs(S1,U,T2),time(T2),necessary(S2,R,inc,I2,A,U),allSupRel(R),sameHistory(T1,S1,S2),sameAgent(I,A),auto(U),volition(I),action(A),I!=A.
